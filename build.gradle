plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.10'
    id 'application'
    id 'java'
    id 'idea'
}

group 'ch.unibas.dmi.dbis'
version '2.0.0-SNAPSHOT'

mainClassName = 'ch.unibas.dmi.dbis.vrem.VREMKt'
applicationDefaultJvmArgs = ["-Xms2G", "-Xmx4G"]
sourceCompatibility = 1.11

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

application {
    // Main class for the ./gradlew run command.
    mainClass = mainClassName
}

jar {
    manifest {
        attributes 'Version': archiveVersion.get()
        attributes 'Main-Class': mainClassName
        attributes 'Manifest-Version': '1.0'
        attributes 'Multi-Release': true
    }

    // Gradle 7.0 requires a duplicate handling strategy or fails if they occur.
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

idea {
    module {
        downloadJavadoc = true

        excludeDirs += file('data')
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Align versions of all Kotlin components.
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    // CLI.
    implementation group: 'com.github.ajalt', name: 'clikt', version: '2.8.0'
    implementation group: 'com.github.rvesse', name: 'airline', version: '2.8.1'

    // KMongo.
    implementation group: 'org.litote.kmongo', name: 'kmongo-serialization', version: '4.2.7'
    implementation group: 'org.litote.kmongo', name: 'kmongo-id-serialization', version: '4.2.7'

    // Javalin.
    implementation group: 'io.javalin', name: 'javalin', version: '3.13.7'

    // Kotlinx Serialization.
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-json', version: '1.2.1'

    // OpenAPI.
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.0'
    implementation group: 'com.squareup.moshi', name: 'moshi-kotlin', version: '1.11.0'

    // Javax.
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    // Logging.
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-web', version: '2.14.1'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'

    // Kotlin test & JUnit library.
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

task copyResources {
    doLast {
        copy {
            from "config.json"
            into "$buildDir/libs/"
        }
    }
}

task deploy(dependsOn: [build, jar, copyResources])
